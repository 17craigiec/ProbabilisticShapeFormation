PI = 3.14159
first_loop = 1
count = 0

map = { .0 = { .1 = { .67.15 = -0.27}, .2 = { .129.62 = -0.02}, .3 = { .89.64 = -0.86}, .4 = { .120.90 = -1.53}, .5 = { .123.16 = -1.07}, .6 = { .174.81 = -0.80}}, .1 = { .0 = { .67.15 = 2.88}, .2 = { .66.56 = 0.23}, .3 = { .50.80 = -1.70}, .4 = { .119.37 = -2.10}, .5 = { .90.57 = -1.63}, .6 = { .121.69 = -1.08}}, .2 = { .0 = { .129.62 = 3.12}, .1 = { .66.56 = -2.91}, .3 = { .96.81 = -2.40}, .4 = { .172.03 = -2.38}, .5 = { .126.92 = -2.16}, .6 = { .122.61 = -1.63}}, .3 = { .0 = { .89.64 = 2.28}, .1 = { .50.80 = 1.44}, .2 = { .96.81 = 0.74}, .4 = { .75.24 = -2.36}, .5 = { .40.01 = -1.55}, .6 = { .85.57 = -0.73}}, .4 = { .0 = { .120.90 = 1.61}, .1 = { .119.37 = 1.04}, .2 = { .172.03 = 0.76}, .3 = { .75.24 = 0.78}, .5 = { .55.89 = 0.23}, .6 = { .117.67 = -0.03}}, .5 = { .0 = { .123.16 = 2.07}, .1 = { .90.57 = 1.51}, .2 = { .126.92 = 0.98}, .3 = { .40.01 = 1.59}, .4 = { .55.89 = -2.91}, .6 = { .65.39 = -0.26}}, .6 = { .0 = { .174.81 = 2.35}, .1 = { .121.69 = 2.06}, .2 = { .122.61 = 1.51}, .3 = { .85.57 = 2.42}, .4 = { .117.67 = 3.11}, .5 = { .65.39 = 2.88}}}




# Subtracts Vector 1 from Vector 0
function subVectors(theta_0, d_0, theta_1, d_1) {
	x_sub = d_0*math.cos(theta_0) - d_1*math.cos(theta_1)
	y_sub = d_0*math.sin(theta_0) - d_1*math.sin(theta_1)
	vect = {}
	vect.x = x_sub
	vect.y = y_sub
	vect.mag = ((x_sub^2) + (y_sub^2))^0.5	
	return vect
}

function drawGlobalLine(r,g,b, x,y) {
	cur_heading = pose.orientation.yaw
	x_print = x*math.cos(-cur_heading) - y*math.sin(-cur_heading)
	y_print = x*math.sin(-cur_heading) + y*math.cos(-cur_heading)
	debug.rays.add(r,g,b,0.0,0.0,0.1, x_print, y_print, 0.1 )
}

#
# Find the distance between two angles
#
function delta_angle(angle_1, angle_2) {
	d = angle_1 - angle_2
	return math.abs((d + PI) % (2*PI) - PI)
}

#
# Get the confidence of a given measured angle vs a confidence angle
#
ALPHA = 10 # Steepness
BETA = PI/4 # 50% value: range from 0 to PI

function get_confidence(map_vector, measured_vector){
	delta = delta_angle(map_vector.a, measured_vector.theta)
	confidence = 1/(1 + math.exp(ALPHA*(delta-BETA)))

	return confidence
}

#
# Function returns a confidence map
#
function get_confidence_map(){

	confidence_map = {}

	# Evaluate the confidence of the robot for each point in the map normalized from [0, 1]
	foreach(map, function(point, map_edges){

		confidence_map[point] = get_confidence(map_target_vectors[point], cur_avg_vector)

	})

	# Normalize total probabilities from 0 to 1
	total = 0
	foreach(confidence_map, function(point, value){
		total = total + value
	})

	if (total > 0){
		foreach(confidence_map, function(point, value){
			confidence_map[point] = value/total
		})
	}

	return confidence_map

}

function init() {
	math.rng.setseed(id)
  reset()
	map_target_vectors = {}
	blocked_ids = {}

	drive_vector = {}
	drive_vector["mag"] = 9999
	min_neighbor_distance = 9999

	foreach(map, function(point, list){
		avg_ndistance = 0.0
		avg_nangle = 0.0 
		avg_nangle_sin = 0.0
		avg_nangle_cos = 0.0
		index = 0.0
		foreach(list, function(neighbor, innerTable) {
			index = index + 1.0
			foreach(innerTable, function(distance, angle){
			avg_ndistance = avg_ndistance + distance
			avg_nangle_sin = avg_nangle_sin + math.sin(angle)
			avg_nangle_cos = avg_nangle_cos + math.cos(angle)
			})
		})
		avg_ndistance = avg_ndistance/index
		avg_nangle_sin = avg_nangle_sin/index
		avg_nangle_cos = avg_nangle_cos/index

		avg_nangle = math.atan(avg_nangle_sin, avg_nangle_cos)
		map_target_vectors[point] = {.d = avg_ndistance, .a = avg_nangle}

		blocked_ids[point] = 0
	}) 
}

function step() {
	count = count + 1
	cur_heading = pose.orientation.yaw
	obst = reduce(proximity,
		function(idx,sensor,sofar) {
			return {
				.x = sofar.x + sensor.value * math.cos(sensor.angle),
				.y = sofar.y + sensor.value * math.sin(sensor.angle)
			}}, { .x = 0.0, .y = 0.0 })

	min_neighbor_distance = 9999

	if (neighbors.count() > 0){
	avg_distance = 0.0
	avg_angle = 0.0
	avg_angle_sin = 0.0
	avg_angle_cos = 0.0
	i = 0.0
	neighbors.foreach(
  	function(rid, data) {
			 i = i + 1
       avg_distance = avg_distance + data.distance
			 avg_angle_sin = avg_angle_sin + math.sin(data.azimuth)
			 avg_angle_cos = avg_angle_cos + math.cos(data.azimuth)
			 if(data.distance < min_neighbor_distance) {
			 		min_neighbor_distance = data.distance
			 }
 		})
	avg_distance = avg_distance/i
	avg_angle = math.atan(avg_angle_sin/i, avg_angle_cos/i)
	tmp_cur_x = avg_distance*math.cos(avg_angle)
	tmp_cur_y = avg_distance*math.sin(avg_angle)

	cur_avg_vector = {}
	cur_avg_vector.x = tmp_cur_x*math.cos(cur_heading) - tmp_cur_y*math.sin(cur_heading)
	cur_avg_vector.y = tmp_cur_x*math.sin(cur_heading) + tmp_cur_y*math.cos(cur_heading)
	cur_avg_vector.mag = ((cur_avg_vector.x)^2+(cur_avg_vector.y)^2)^0.5
	cur_avg_vector.theta = math.atan(cur_avg_vector.y,cur_avg_vector.x)

	debug.print(cur_heading*180/PI)
	drawGlobalLine(205,50,0, cur_avg_vector.x/100,cur_avg_vector.y/100)

	confidence_map = get_confidence_map()

	correction_vectors = {}	
	drive_vector.mag = 9999
	foreach(map_target_vectors,
  	function(rid, innerTable) {
			correction_vectors[rid] = subVectors(cur_avg_vector.theta, cur_avg_vector.mag, innerTable.a, innerTable.d)
			if(correction_vectors[rid].mag < drive_vector.mag and blocked_ids[rid] == 0 and count%4 == 1) {
				drive_vector = correction_vectors[rid]
				drive_vector.id = rid
			}
		})

		move(drive_vector.x, drive_vector.y, 0)

		# Switch Handling
		P = 0.01
		rng_num = math.rng.uniform(1.0)
		if(min_neighbor_distance < 20) {
			if(rng_num < P) {
				blocked_ids[drive_vector.id] = 100
			}
		}

		decrementBlockedIdCounter()
		#move(close_est.x,close_est.y)  
		drawGlobalLine(0,205,50, drive_vector.x/100, drive_vector.y/100)
	}
}


function decrementBlockedIdCounter()
{
	foreach(blocked_ids,
  	function(id, counter) {
			if(counter > 0) {
				blocked_ids[id] = blocked_ids[id] - 1
			}
	})
}


function moveTo(id)
{
	move(correction_vectors[id].x, correction_vectors[id].y, 0)
}


function moveWithConfidence()
{
	switch_chance = math.rng.uniform(1.0)
	if(switch_chance < P or first_loop)
	{
		total = 0
		rng_num = math.rng.uniform(1.0)
		foreach(confidence_map, function(point, conf){
			if(total <= rng_num and rng_num < total+conf)
			{
				confidence_vector = correction_vectors[point]
				target_goal = point
				positional_error = confidence_vector.mag
				move(confidence_vector.x, confidence_vector.y, 0)	
			}
			first_loop = 0
			total = total + conf
		})
	}else
	{
		move(correction_vectors[point].x, correction_vectors[point].y, 0)
	}
}


function move(x, y, diffuse){
	if(diffuse){
		if((x < 0.0001) and (y < 0.0001))
		{
			gotoc(7, 0) 
		}else{
			angle = math.atan(x,y)
		if((angle > 0) and (3.14 > angle))
		{
			set_wheels(-7.0, 7.0)
		}	
		else
		{		 	
			set_wheels(7.0, -7.0)
		}
		}
	}
	else{
		desired_angle = math.atan(y, x)
		error = pose.orientation.yaw - desired_angle
		error_threshold = 0.04
		if(math.abs(error) < error_threshold)
		{
			set_wheels(10.0,10.0)
		}
		else if (error > 0){
			set_wheels(5.0,-5.0)
		}
		else{
			set_wheels(-5.0,5.0)
		} 
		debug.print("Err:", error, "   da:", desired_angle)
	}
}


#
# Executed once upon resetting
#
function reset() {

}

#
# Executed once upon closing ARGoS
#
function destroy() {
  # Nothing to do
}

